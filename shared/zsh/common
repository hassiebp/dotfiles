# Common aliases
alias python=python3
alias vim=nvim
alias pip=pip3

alias lg=lazygit

# aliases
alias glop='git log --oneline --graph --decorate --date=short --pretty=format:"%C(auto)%h %C(green)(%ad) %C(yellow)%d%Creset %s %C(bold blue)<%an>%Creset"'

gbf() {
  local branch
  branch=$(git branch --color=always \
    | rg -v '^\*' \
    | sed 's/^..//' \
    | fzf --height=42% --reverse --ansi --query="$1" \
          --preview 'git log --color=always --decorate --abbrev-commit --date=short --pretty=format:"%C(auto)%h %ad %d %s" -n 10 {}' \
          --preview-window=right:62%)

  [[ -n "$branch" ]] && git checkout "${branch##*/}"
}

vimf() {
  local file
  file=$(fzf \
    --query="$1" \
    --height=62% \
    --reverse \
    --info=inline \
    --preview 'bat --style=numbers --color=always --line-range :200 {}' \
    --preview-window=right:62%)

  [[ -n "$file" ]] && nvim "$file" "${@:2}"
}

rgf() {
  local pick file line
  pick=$(
    rg --no-heading --line-number --column --color=always "${1:-.}" \
    | fzf --ansi --reverse --height=62% --delimiter=':' \
          --preview 'bash -c '\''file="$1"; line="$2";
            start=$((line-15)); (( start<1 )) && start=1
            end=$((line+15))
            bat --style=numbers --color=always --line-range ${start}:${end} \
                --highlight-line ${line} "$file"'\'' -- {1} {2}' \
          --preview-window=right:62%
  ) || return

  file=$(echo "$pick" | cut -d: -f1)
  line=$(echo "$pick" | cut -d: -f2)
  [[ -n "$file" && -n "$line" ]] && nvim "+${line}" "$file"
}

gdf() {
  local file
  file=$(git status --porcelain | awk '{print $2}' \
        | fzf --reverse --height=42% --preview 'git diff --color=always -- {1}' \
              --preview-window=right:62%)
  [[ -n "$file" ]] && git diff --color=always -- "$file"
}

zf() {
  local dir
  dir=$(zoxide query -l | fzf --reverse --height=42% --preview 'ls -la {} | head -200' --preview-window=right:60%)
  [[ -n "$dir" ]] && cd "$dir"
}

scratch() {
  local ext="md"
  local name=""

  # Parse options
  while [[ $# -gt 0 ]]; do
    case "$1" in
      -e|--extension)
        ext="${2#.}"  # remove leading dot if provided
        shift 2
        ;;
      -*)
        echo "Unknown option: $1" >&2
        echo "Usage: scratch [name] [-e|--extension EXT]" >&2
        return 1
        ;;
      *)
        name="-$1"
        shift
        ;;
    esac
  done

  local dir="$HOME/scratch"
  mkdir -p "$dir"

  local ts
  ts=$(date +%Y%m%d-%H%M%S)
  local file="$dir/${ts}${name}.$ext"

  $EDITOR "$file"
}

scf() {
  local file
  file=$(ls $HOME/scratch | fzf --reverse --height=42% --preview 'bat $HOME/scratch/{} | head -200' --preview-window=right:60%)

  $EDITOR $HOME/scratch/$file
}

# tmux
alias tm=tmux
alias tma='tmux attach'
alias tml='tmux ls'
alias tmd='tmux detach'
alias tmks='tmux kill-session'

alias tg=terragrunt

# Common env variables
export EDITOR=nvim
export VISUAL=nvim
export MANPAGER='nvim +Man!'

# Lazy-load NVM (saves ~500ms shell startup time)
export NVM_DIR="$HOME/.nvm"
if [ -s "$NVM_DIR/nvm.sh" ]; then
  __load_nvm() {
    unset -f node npm npx nvm __load_nvm
    [ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"
    [ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"
  }
  node() { __load_nvm && node "$@"; }
  npm() { __load_nvm && npm "$@"; }
  npx() { __load_nvm && npx "$@"; }
  nvm() { __load_nvm && nvm "$@"; }
fi

# pyenv configuration (if installed)
export PYENV_ROOT="$HOME/.pyenv"
[[ -d $PYENV_ROOT/bin ]] && export PATH="$PYENV_ROOT/bin:$PATH"
command -v pyenv >/dev/null && eval "$(pyenv init -)"


export FZF_DEFAULT_COMMAND='fd --type file --strip-cwd-prefix --color=always'
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
export FZF_DEFAULT_OPTS="--ansi"

# Shell history
export HISTSIZE=10000
export SAVEHIST=100000
setopt INC_APPEND_HISTORY    # Write to history immediately, not on shell exit
setopt SHARE_HISTORY         # Share history between all sessions

# fzf key bindings
source <(fzf --zsh)

# eza alias (if installed)
if command -v eza >/dev/null 2>&1; then
  alias ls=eza
  alias ll="eza --long"
  alias lt="eza --tree"
  alias llt="eza --long --tree"
fi
